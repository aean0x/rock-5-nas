{ pkgs, ... }:
let
  baseImage = "ghcr.io/phioranex/openclaw-docker:latest";
  customImage = "openclaw-custom:latest";

  # ── Editable dependency lists ────────────────────────────────
  # prettier-ignore
  aptPackages = [
    "git"
    "curl"
    "jq"
    "nodejs"
    "python3-pip"
    "python3-venv"
    "ffmpeg"
    "build-essential"
    "ca-certificates"
  ];

  pipPackages = [
    # "some-package"
  ];

  npmPackages = [
    "@steipete/bird"
    "playwright"
  ];

  pnpmPackages = [
    "@clawdbot/lobster"
  ];

  uvPackages = [
    # "some-package"
  ];

  # Full npx commands to run after package installs (each becomes its own RUN)
  npxCommands = [
    "playwright install --with-deps chromium"
  ];

  # ── Dockerfile fragments ────────────────────────────────────
  mkPkgStep =
    cmd: pkgList:
    if pkgList == [ ] then
      ""
    else
      ''
        RUN ${cmd} ${builtins.concatStringsSep " " pkgList}
      '';

  aptLine = builtins.concatStringsSep " " aptPackages;
  pipStep = mkPkgStep "pip install --no-cache-dir --break-system-packages" pipPackages;
  npmStep = mkPkgStep "npm install -g" npmPackages;
  uvStep = mkPkgStep "uv pip install --system" uvPackages;
  pnpmStep =
    if pnpmPackages == [ ] then
      ""
    else
      ''
        RUN PNPM_HOME=/usr/local/bin pnpm add -g ${builtins.concatStringsSep " " pnpmPackages}
      '';
  npxStep =
    if npxCommands == [ ] then
      ""
    else
      builtins.concatStringsSep "" (
        map (cmd: ''
          RUN npx ${cmd}
        '') npxCommands
      );

in
{
  # Build custom image on-device (native docker build, no qemu)
  systemd.services.openclaw-builder = {
    description = "Build custom OpenClaw image with Docker CLI";
    before = [
      "docker-openclaw-gateway.service"
      "docker-openclaw-cli.service"
    ];
    requiredBy = [
      "docker-openclaw-gateway.service"
      "docker-openclaw-cli.service"
    ];
    path = [
      pkgs.docker
      pkgs.gnutar
      pkgs.coreutils
    ];
    script = ''
      docker build -t ${customImage} - <<'EOF'
      FROM ${baseImage}
      USER root

      # System packages
      RUN rm -rf /var/lib/apt/lists/* /var/cache/apt/archives/* && \
          apt-get update && \
          apt-get install -y --no-install-recommends ${aptLine} && \
          rm -rf /var/lib/apt/lists/* /var/cache/apt/archives/*

      # uv + uvx
      RUN curl -fsSL https://github.com/astral-sh/uv/releases/download/0.5.0/uv-aarch64-unknown-linux-gnu.tar.gz | \
          tar -xzf - --strip-components=1 -C /usr/local/bin \
            uv-aarch64-unknown-linux-gnu/uv uv-aarch64-unknown-linux-gnu/uvx

      # Docker CLI
      RUN curl -fsSL https://download.docker.com/linux/static/stable/aarch64/docker-26.1.3.tgz | \
          tar -xzf - --strip-components=1 -C /usr/local/bin docker/docker

      # goplaces
      RUN curl -fsSL https://github.com/steipete/goplaces/releases/download/v0.3.0/goplaces_0.3.0_linux_arm64.tar.gz | \
          tar -xzf - -C /usr/local/bin goplaces

      # Ensure all extracted binaries are executable
      RUN chmod +x /usr/local/bin/uv /usr/local/bin/uvx /usr/local/bin/docker /usr/local/bin/goplaces

      # Dependency install steps (pip / npm / pnpm / uv / npx)
      ${pipStep}${npmStep}${pnpmStep}${uvStep}${npxStep}

      # Docker group
      RUN groupadd -g 131 docker 2>/dev/null || true && \
          usermod -aG docker node

      # Directory setup
      RUN mkdir -p /var/lib/apt/lists/partial /var/cache/apt/archives/partial \
                   /home/node/.cache/uv /home/node/.local/share/uv \
                   /tmp /dev/shm /var/tmp/openclaw-compile-cache && \
          chown -R 1000:1000 /home/node /tmp /var/tmp/openclaw-compile-cache && \
          chmod -R 1777 /tmp /dev/shm && \
          chown -R _apt:root /var/lib/apt /var/cache/apt 2>/dev/null || true

      # OpenClaw CLI wrapper
      RUN printf '#!/bin/sh\nexec node /app/dist/index.js "$@"\n' > /usr/local/bin/openclaw && \
          chmod +x /usr/local/bin/openclaw
      RUN git config --global --add safe.directory '*'

      USER 1000:1000
      EOF

      # Extract tools into workspace so sandbox binds stay within allowed roots
      mkdir -p /home/node/.openclaw/workspace/.tools
      docker run --rm --entrypoint sh ${customImage} -c 'tar cf - /usr/local/bin/' | \
        tar xf - --strip-components=3 -C /home/node/.openclaw/workspace/.tools
      chown -R 1000:1000 /home/node/.openclaw/workspace/.tools
      chmod -R +x /home/node/.openclaw/workspace/.tools
    '';
    serviceConfig = {
      Type = "oneshot";
      TimeoutStartSec = "300";
    };
  };
}
